(ns gijiroku.whisper-test
  "Offline-checkable pieces of the Whisper client: verbose_json segment
  normalization and the multipart/form-data body shape. The HTTP round trip
  itself needs a real API key and is not exercised here (see gijiroku.whisper
  namespace docstring)."
  (:require [clojure.test :refer [deftest is]]
            [kotoba.lang.text :as str]
            [gijiroku.whisper :as w]
            [gijiroku.transcriber :as t])
  (:import [java.nio.file Files]))

(deftest segment-normalization-drops-diarization
  (let [seg (w/segment->model {:id 0 :text "  進捗を共有します。  " :start 0.0 :end 5.2})]
    (is (= "0" (:seg-id seg)))
    (is (nil? (:speaker seg)) "verbose_json has no speaker field")
    (is (= 0 (:t0 seg)))
    (is (= 5 (:t1 seg)))
    (is (= "進捗を共有します。" (:text seg)))))

(deftest multipart-body-is-well-formed
  (let [f (Files/createTempFile "gijiroku-whisper-test" ".wav" (make-array java.nio.file.attribute.FileAttribute 0))
        _ (Files/write f (.getBytes "RIFF....WAVEfmt ") (make-array java.nio.file.OpenOption 0))
        bnd "TESTBOUNDARY"
        body (String. ^bytes (#'gijiroku.whisper/multipart-body bnd {:model "whisper-1" :response_format "verbose_json"}
                                                                 (str f)))]
    (is (str/includes? body (str "--" bnd)))
    (is (str/includes? body "name=\"model\""))
    (is (str/includes? body "whisper-1"))
    (is (str/includes? body "name=\"file\""))
    (is (str/includes? body "Content-Type: audio/wav"))
    (is (str/ends-with? body (str "--" bnd "--\r\n")))
    (Files/delete f)))

(deftest mock-transcriber-yields-model-shaped-transcript
  (let [transcript (t/-transcribe (t/mock-transcriber) "/tmp/whatever.wav")]
    (is (= :stt-generated (:source transcript)))
    (is (seq (:segments transcript)))
    (is (every? #(nil? (:speaker %)) (:segments transcript)))))
